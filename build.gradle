buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
}

plugins {
    id 'maven-publish'
    id 'fabric-loom' version '0.12-SNAPSHOT'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

version = "${project.minecraft_version}-${project.mod_version}"
archivesBaseName = "${project.modid}"
group = project.maven_group

loom {
    accessWidenerPath = file("src/main/resources/asynclocator.accesswidener")
}

repositories {
    maven { url "https://api.modrinth.com/maven/" }
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "maven.modrinth:midnightlib:${project.midnightlib_version}"
    modRuntimeOnly "maven.modrinth:midnightlib:${project.midnightlib_version}"

    modImplementation ("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}") {
        force = true //Force the version we want rather than trusting Gradle to pick it over transitive suggestions
    }

    modImplementation ("maven.modrinth:modmenu:${project.mod_menu_version}") { transitive = false }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 17
}

java {
    withSourcesJar()
}

jar {
    from "LICENSE"
}

task apiJar(type: Jar, dependsOn: classes) {
    from(sourceSets.main.output)
    include 'asynclocator/**'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}
